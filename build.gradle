
repositories {
  jcenter()
}

subprojects {

  apply (plugin: "scala")

  configurations {
    scalaCompilerPlugin
  }

  dependencies {
    // Use Scala 2.11 in our library project
    compile("org.scala-lang:scala-library:${Deps.scalaVersion}")
    scalaCompilerPlugin("org.scalamacros:paradise_${Deps.scalaVersion}:2.1.1")


    testCompile("junit:junit:4.12")
    testCompile("org.scalatest:scalatest_${Deps.scalaBaseVersion}:3.0.5")
    testCompile("org.scalacheck:scalacheck_${Deps.scalaBaseVersion}:1.14.0")
    // Need scala-xml at test runtime
    testRuntime("org.scala-lang.modules:scala-xml_${Deps.scalaBaseVersion}:1.1.0")
  }


  repositories {
    jcenter()
  }

  tasks.withType(ScalaCompile) {
    // Map plugin jars to -Xplugin parameter
    List<String> parameters =
      configurations.scalaCompilerPlugin.files.collect {
        "-Xplugin:" + it.absolutePath
      }

    // Add existing parameters
    List<String> existingParameters = scalaCompileOptions.additionalParameters
    if (existingParameters) {
      parameters.addAll(existingParameters)
    }

    // Add whatever flags you typically add
    parameters += [
      "-language:implicitConversions",
      "-language:higherKinds"
    ]

    // Finally set the additionalParameters
    scalaCompileOptions.additionalParameters = parameters
  }
}
